plugins {
    id 'application'
}

version = '0.0.1'

application {
    mainClass = 'litresbot.Application'
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.25.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.25.0'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.25.0'
    implementation 'org.slf4j:slf4j-reload4j:2.0.17'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'com.zaxxer:HikariCP:6.3.0'
    implementation 'software.amazon.awssdk:s3:2.31.33'
    implementation 'org.telegram:telegrambots:6.9.7.1'
    implementation 'org.jsoup:jsoup:1.20.1'
    implementation 'org.apache.commons:commons-text:1.13.1'

    // testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.17.0'
}

def generatedVersionDir = "${buildDir}/resources/main/"

sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty('version', rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}